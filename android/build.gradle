buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['SquareInAppPayments_' + name]
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}


apply plugin: "com.android.library"
apply plugin: "kotlin-android"

apply plugin: "com.facebook.react"

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["SquareInAppPayments_" + name]).toInteger()
}

android {
  namespace "com.squareinapppayments"

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      java.srcDirs += [
        "generated/java",
        "generated/jni",
        "$buildDir/generated/source/codegen/java",
        "$buildDir/generated/source/codegen/jni"
      ]
    }
  }
}

repositories {
  mavenCentral()
  google()
}

rootProject.allprojects {
  repositories {
    maven {
      url 'https://sdk.squareup.com/public/android'
    }
  }
}

def kotlin_version = getExtOrDefault("kotlinVersion")
def sqipVersion = getExtOrDefault("sqipVersion")
def googlePayVersion = getExtOrDefault("googlePayVersion")

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "com.google.android.gms:play-services-wallet:$googlePayVersion"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation "com.squareup.sdk.in-app-payments:card-entry:$sqipVersion"
  implementation "com.squareup.sdk.in-app-payments:google-pay:$sqipVersion"
  implementation "com.squareup.sdk.in-app-payments:buyer-verification:$sqipVersion"
}
